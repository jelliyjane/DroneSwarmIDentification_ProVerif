(* Symmetric encryption *)
type key.

fun senc(bitstring, key): bitstring.

reduc forall m: bitstring, k:key; sdec(senc(m,k),k)=m.

(* ECC key *)
type ec_skey.
type ec_pkey.

(* Message authentication code *)
fun mac_h(key,ec_pkey,bitstring,ec_pkey): bitstring.

(* Hash function *)
fun h(key,ec_pkey): bitstring.

(* typeConverter Function *)
fun ec_pkey_to_b(ec_pkey):bitstring [data, typeConverter].
fun ec_skey_to_b(ec_skey):bitstring [data, typeConverter].

(* ECC Multiplication operation *)
fun ECMul(bitstring, bitstring): bitstring.

(* ECC Add operation *)
fun ECAdd(bitstring, bitstring): bitstring.

const P:bitstring.
free c:channel.
free gk:key [private].
query attacker(gk).

event acceptDrone(bitstring).
event termDrone(bitstring).
event acceptRDrone(bitstring,ec_pkey).
event termRDrone(bitstring,ec_pkey).

query x:bitstring; inj-event(termDrone(x))==>inj-event(acceptDrone(x)).
query x:bitstring, y:ec_pkey; inj-event(termRDrone(x,y))==>inj-event(acceptRDrone(x,y)).

let drone(id_D:bitstring,id_R:bitstring, A:ec_pkey,r:ec_skey,pkR:ec_pkey,skD:ec_skey) =
    let mac = mac_h(gk,A,id_D,pkR) in
    out(c, (id_D, A, mac));
    event acceptDrone(mac);
    in(c, msg2:bitstring);
    let (T:ec_pkey, =id_R, =A) = sdec(msg2, gk) in
    event termRDrone(id_R, A);
    out(c,ECAdd(ECMul(h(gk,T), ec_pkey_to_b(A)),ECMul(ec_skey_to_b(skD),ec_pkey_to_b(T)))).
    
let reader(id_D:bitstring, id_R:bitstring, A:ec_pkey,pkR:ec_pkey,T:ec_pkey) =
    in(c, msg1:bitstring);
    let(=id_D, A: ec_pkey, mac:bitstring) = msg1 in
    let(=mac_h(gk,A,id_D,pkR)) = mac in
    event termDrone(mac);
    out(c, senc((T,id_R,A),gk));
    event acceptRDrone(id_R,A);
    in(c, B:bitstring).

process
    new id_D:bitstring;
    new id_R:bitstring;
    new A:ec_pkey;
    new pkR:ec_pkey;
    new T:ec_pkey;
    new r:ec_skey;
    new skD:ec_skey;
    new gk:key;
    
    ((!drone(id_D,id_R,A,r,pkR,skD))|(!reader(id_D,id_R,A,pkR,T)))
